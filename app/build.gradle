import java.text.SimpleDateFormat

def BURUBERI_VERSION = '1.0.2'
def COMMON_SENSE_VERSION = '2016.11.7.2'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'me.tatarka:gradle-retrolambda:3.2.1'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def generateVersionCode() {
    def now = new Date()
    def formatter = new SimpleDateFormat("yyMMddHH")
    Integer.parseInt(formatter.format(now))
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "is.hello.puppet"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode generateVersionCode()
        versionName "1.3.1"
    }
    buildTypes {
        def TYPE_STRING = "String"

        debug {
            buildConfigField TYPE_STRING, "BURUBERI_VERSION", '"' + BURUBERI_VERSION + '"'
            buildConfigField TYPE_STRING, "COMMON_SENSE_VERSION", '"' + COMMON_SENSE_VERSION + '"'
        }
        release {
            buildConfigField TYPE_STRING, "BURUBERI_VERSION", '"' + BURUBERI_VERSION + '"'
            buildConfigField TYPE_STRING, "COMMON_SENSE_VERSION", '"' + COMMON_SENSE_VERSION + '"'

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'io.reactivex:rxjava:1.0.9'
    compile "is.hello:buruberi-core:$BURUBERI_VERSION"
    compile "is.hello.commonsense:ble:$COMMON_SENSE_VERSION"

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Necessary to be able to compile and run tests with
    // Android Studio 1.3 + build tools 1.3. Remove when
    // retrolambda releases an update.
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
