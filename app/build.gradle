import java.text.SimpleDateFormat

def BURUBERI_VERSION = '0.1.0'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }
    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'me.tatarka:gradle-retrolambda:3.2.1'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def generateVersionCode() {
    def now = new Date()
    def formatter = new SimpleDateFormat("yyMMddHH")
    Integer.parseInt(formatter.format(now))
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "is.hello.puppet"
        minSdkVersion 19
        targetSdkVersion 22
        versionCode generateVersionCode()
        versionName "1.1.3"
    }
    buildTypes {
        def TYPE_STRING = "String"

        debug {
            buildConfigField TYPE_STRING, "BURUBERI_VERSION", '"' + BURUBERI_VERSION + '"'
        }
        release {
            buildConfigField TYPE_STRING, "BURUBERI_VERSION", '"' + BURUBERI_VERSION + '"'

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.0.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'net.danlew:android.joda:2.7.2'

    compile 'io.reactivex:rxjava:1.0.9'

    compile "is.hello.buruberi:core:$BURUBERI_VERSION"

    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Necessary to be able to compile and run tests with
    // Android Studio 1.3 + build tools 1.3. Remove when
    // retrolambda releases an update.
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}
